#type common
#version 460 core
/*
 * Shader for the directional light widget in the Strontium editor.
 * TODO: Move away from pushed uniforms and use UBOs.
 */

#type vertex
layout (location = 0) in vec4 vPosition;
layout (location = 1) in vec3 vNormal;
layout (location = 2) in vec3 vColour;
layout (location = 3) in vec2 vTexCoord;
layout (location = 4) in vec3 vTangent;
layout (location = 5) in vec3 vBitangent;

uniform mat4 mVP;
uniform mat3 normalMat;
uniform mat4 model;

// Vertex properties for shading.
out VERT_OUT
{
  vec3 fNormal;
  vec3 fPosition;
  vec3 fColour;
  vec2 fTexCoords;
  mat3 fTBN;
} vertOut;

void main()
{
  // Tangent to world matrix calculation.
  vec3 T = normalize(vec3(normalMat * vTangent));
  vec3 B = normalize(vec3(normalMat * vBitangent));
  vec3 N = normalize(vec3(normalMat * vNormal));

  gl_Position = mVP * vPosition;
  vertOut.fPosition = (model * vPosition).xyz;
  vertOut.fNormal = normalMat * vNormal;
  vertOut.fColour = vColour;
  vertOut.fTexCoords = vTexCoord;
  vertOut.fTBN = mat3(T, B, N);
}

#type fragment
#define PI 3.141592654
#define THRESHHOLD 0.00005

uniform vec3 camPos = vec3(2.0);

// Directional light uniforms.
uniform vec3 lDirection = vec3(0.0, -1.0, 0.0);
uniform vec3 lColour = vec3(1.0);
uniform float lIntensity = 1.0;

in VERT_OUT
{
  vec3 fNormal;
  vec3 fPosition;
  vec3 fColour;
  vec2 fTexCoords;
  mat3 fTBN;
} fragIn;

// Output colour variable.
layout(location = 0) out vec4 fragColour;

//------------------------------------------------------------------------------
// Janky LearnOpenGL PBR.
//------------------------------------------------------------------------------
// Trowbridge-Reitz distribution function.
float TRDistribution(vec3 N, vec3 H, float alpha);
// Smith-Schlick-Beckmann geometry function.
float SSBGeometry(vec3 N, vec3 L, vec3 V, float roughness);
// Schlick approximation to the Fresnel factor.
vec3 SFresnel(float cosTheta, vec3 F0);
// Schlick approximation to the Fresnel factor, with roughness!
vec3 SFresnelR(float cosTheta, vec3 F0, float roughness);

void main()
{
  vec3 position = fragIn.fPosition;
  vec3 normal = fragIn.fNormal;
  vec3 albedo = vec3(1.0);
  float metallic = 0.0;
  float roughness = 1.0;

  vec3 F0 = mix(vec3(0.04), albedo, metallic);

  vec3 view = normalize(position - camPos);
  vec3 light = normalize(lDirection);
  vec3 halfWay = normalize(view + light);

  float NDF = TRDistribution(normal, halfWay, roughness);
  float G = SSBGeometry(normal, view, light, roughness);
  vec3 F = SFresnel(max(dot(halfWay, view), THRESHHOLD), F0);

  vec3 kD = (vec3(1.0) - F) * (1.0 - metallic);

  vec3 num = NDF * G * F;
  float den = 4.0 * max(dot(normal, view), THRESHHOLD) * max(dot(normal, light), THRESHHOLD);
  vec3 spec = num / max(den, THRESHHOLD);

  vec3 ambient = 0.5 * vec3(1.0);

  fragColour = vec4((kD * albedo / PI + spec) * lColour * lIntensity * max(dot(normal, light) + ambient, THRESHHOLD), 1.0);
}

// Trowbridge-Reitz distribution function.
float TRDistribution(vec3 N, vec3 H, float roughness)
{
  float alpha = roughness * roughness;
  float a2 = alpha * alpha;
  float NdotH = max(dot(N, H), THRESHHOLD);
  float NdotH2 = NdotH * NdotH;

  float nom = a2;
  float denom = (NdotH2 * (a2 - 1.0) + 1.0);
  denom = PI * denom * denom;

  return nom / denom;
}

// Schlick-Beckmann geometry function.
float Geometry(float NdotV, float roughness)
{
  float r = (roughness + 1.0);
  float k = (roughness * roughness) / 8.0;

  float nom   = NdotV;
  float denom = NdotV * (1.0 - k) + k;

  return nom / denom;
}

// Smith's modified geometry function.
float SSBGeometry(vec3 N, vec3 L, vec3 V, float roughness)
{
  float NdotV = max(dot(N, V), THRESHHOLD);
  float NdotL = max(dot(N, L), THRESHHOLD);
  float g2 = Geometry(NdotV, roughness);
  float g1 = Geometry(NdotL, roughness);

  return g1 * g2;
}

// Schlick approximation to the Fresnel factor.
vec3 SFresnel(float cosTheta, vec3 F0)
{
  return F0 + (1.0 - F0) * pow(max(1.0 - cosTheta, THRESHHOLD), 5.0);
}

vec3 SFresnelR(float cosTheta, vec3 F0, float roughness)
{
  return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(max(1.0 - cosTheta, THRESHHOLD), 5.0);
}
